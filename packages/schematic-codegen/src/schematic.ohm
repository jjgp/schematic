Schema {
  /* TODO: optional, readonly members, indexing, lists, ... */
  
  Document = Definition*
  
  Definition = Componentdef | Interfacedef | Typedef

  Interfacedef = interface identifier "{" (StaticMethod | StaticCallable | StaticMember | Method | Member | Callable)* "}"
  
  StaticMethod = static Method
  StaticCallable = static Callable
  StaticMember = static Member
  Method = identifier Callable
  Callable = "(" listOf<NamedValue, ","> ")" "->" (value | void) ";"
  
  Componentdef = component identifier "{" Member* "}"

  Member = identifier ":" (Function | value) ";"

  Typedef = type identifier "{" Property* "}"
  
  Property = NamedValue ";"
  
  Function = "(" listOf<value, ","> ")" "->" (value | void)
  
  NamedValue = identifier ":" value 

  /* TODO: listValue, enumValue, etc.? */
  value = primitive
        | typeValue
        | unknown

  typeValue = identifier

  identifier = (letter | "_") (alnum | "_")*

  /* TODO: should there be reserved words? */
  reserved = component | interface | type

  static = "static"
  init = "init"

  interface = "interface"
  component = "component"
  type = "type"
  
  unknown = "unknown"
  void = "void"

  primitive = boolean | number | string
  
  string = "string"
  number = "number"
  boolean = "boolean"

  comment = singleLineComment | multiLineComment
  multiLineComment = "/*" (~"*/" any)* "*/"
  singleLineComment = "//" (~eol any)* eol
  eol = newline | "\r"
  newline = "\n"
  space += comment
}
